{describe, it, expect, to_equal} = import '@fink/jest/test.fnk'

{
  repeat, count, enumerate, zip, cycle, chain, reverse, sort, unique, flatten
  length, is_empty, join, slice, insert_at, take, drop, compress,
  find_index, is_iter, product, permutations, combinations, equals, pairs, chunks,
  range
} = import './iter.fnk'

{pad_start} = import './str.fnk'



describe 'iter - generating funcs', fn:
  it 'repeats item forever', fn:
    [a, b, c] = '.' | repeat 3

    expect
      [a, b, c]
      to_equal
        ['.', '.', '.']

  it 'counts', fn:
    [a, b, c] = count 0, 2
    expect
      [a, b, c]
      to_equal
        [0, 2, 4]

  it 'creates ranges', fn:
    expect
      [...range 3]
      to_equal
        [0, 1, 2]

    expect
      [...range 1, 5]
      to_equal
        [1, 2, 3, 4]



describe 'iter - transforming funcs', fn:
  it 'enumerates', fn:
    expect
      [... 'abc' | enumerate _]
      to_equal list:
        [0, 'a']
        [1, 'b']
        [2, 'c']

    expect
      [... 'abc' | enumerate 0, -1]
      to_equal list:
        [0, 'a']
        [-1, 'b']
        [-2, 'c']


  it 'zips iterables into one', fn:
    items = zip 'abc', count 1
    expect
      [...items]
      to_equal list:
        ['a', 1]
        ['b', 2]
        ['c', 3]

    expect
      [...items]
      to_equal list:
        ['a', 1]
        ['b', 2]
        ['c', 3]


  it 'cycles', fn:
    [a, b, c, d, e] = cycle [1, 2, 3]

    expect
      [a, b, c, d, e]
      to_equal
        [1, 2, 3, 1, 2]


  it 'chains multiple iterables', fn:
    expect
      [...'abc' | chain [1, 2, 3]]
      to_equal
        ['a', 'b', 'c', 1, 2, 3]


  it 'inserts items into iterable', fn:
    expect
      pipe '123456':
        insert_at 3, 'abc'
        join ''
      to_equal
        '123abc456'

    expect
      pipe '1':
        insert_at 0, 'abc'
        join ''
      to_equal
        'abc1'

    expect
      pipe '1':
        insert_at 1, 'abc'
        join ''
      to_equal
        '1abc'


  it 'creates unique set of items', fn:
    expect
      pipe [1, 2, 2, 3]:
        unique
        [...?]
      to_equal
        [1, 2, 3]


  it 'flattens nested iterable', fn:
    expect
      list:
        ...pipe 'abc':
          enumerate _
          flatten
      to_equal
        [0, 'a', 1, 'b', 2, 'c']


  it 'reverses iterable', fn:
    expect
      reverse [1, 2, 3]
      to_equal
        [3, 2, 1]


  it 'sorts iterable', fn:
    expect
      pipe ['1', '222', '2', '11']:
        sort fn a, b:
          padded_a = pad_start a, 10, ' '
          padded_b = pad_start b, 10, ' '
          match padded_a:
            ? == padded_b: 0
            ? > padded_b: 1
            else: -1

      to_equal
        ['1', '2', '11', '222']


  it 'compresses items', fn:
    expect
      pipe [1, 2, 3, 4]:
        compress [true, false, false, true]
        [...?]
      to_equal
        [1, 4]


  it 'creates a product from iterables', fn:
    expect
      [...product '123']
      to_equal
        list:
          ['1'], ['2'], ['3']


    expect
      [...product '123', 'abc']
      to_equal
        list:
          ['1', 'a'], ['1', 'b'], ['1', 'c']
          ['2', 'a'], ['2', 'b'], ['2', 'c']
          ['3', 'a'], ['3', 'b'], ['3', 'c']

    expect
      pipe product '123', 'abc', '.•˚':
        map items: items | join ''
        [...?]
      to_equal
        list:
          '1a.', '1a•', '1a˚'
          '1b.', '1b•', '1b˚'
          '1c.', '1c•', '1c˚'
          '2a.', '2a•', '2a˚'
          '2b.', '2b•', '2b˚'
          '2c.', '2c•', '2c˚'
          '3a.', '3a•', '3a˚'
          '3b.', '3b•', '3b˚'
          '3c.', '3c•', '3c˚'


  it 'creates permutations', fn:
    expect
      pipe 'abcd':
        permutations _
        map items: items | join ''
        [...?]
      to_equal
        list:
          'abcd', 'abdc', 'acbd', 'acdb', 'adbc', 'adcb'
          'bacd', 'badc', 'bcad', 'bcda', 'bdac', 'bdca'
          'cabd', 'cadb', 'cbad', 'cbda', 'cdab', 'cdba'
          'dabc', 'dacb', 'dbac', 'dbca', 'dcab', 'dcba'

    expect
      pipe 'abcd':
        permutations 2
        map items: items | join ''
        [...?]
      to_equal
        ['ab', 'ac', 'ad', 'ba', 'bc', 'bd', 'ca', 'cb', 'cd', 'da', 'db', 'dc']


  it 'creates combinations', fn:
    expect
      pipe 'abcd':
        combinations 1
        map items: items | join ''
        [...?]
      to_equal
        ['a', 'b', 'c', 'd']

    expect
      pipe 'abcd':
        combinations 2
        map items: items | join ''
        [...?]
      to_equal
        ['ab', 'ac', 'ad', 'bc', 'bd', 'cd']

    expect
      pipe 'abcd':
        combinations 3
        map items: items | join ''
        [...?]
      to_equal
        ['abc', 'abd', 'acd', 'bcd']

    expect
      pipe 'abcd':
        combinations 4
        map items: items | join ''
        [...?]
      to_equal
        ['abcd']


  it 'creates pairs', fn:
    expect
      pipe pairs 'ab':
        map items: items | join ''
        [...?]
      to_equal
        ['ab']

    expect
      pipe pairs 'abc':
        map items: items | join ''
        [...?]
      to_equal
        ['ab', 'bc']

    expect
      pipe pairs 'abcd':
        map items: items | join ''
        [...?]
      to_equal
        ['ab', 'bc', 'cd']


  it 'creates chunks', fn:
    expect
      pipe '0123456789':
        chunks 3
        map items: items | join ''
        [...?]
      to_equal
        ['012', '345', '678', '9']

    expect
      pipe '0123':
        chunks 2
        map items: items | join ''
        [...?]
      to_equal
        ['01', '23']

    expect
      pipe '0123':
        chunks 3, 'x'
        map items: items | join ''
        [...?]
      to_equal
        ['012', '3xx']



describe 'slice', fn:
  it 'advances ignores n elements', fn:
    expect
      pipe '1234567890':
        drop 5
        join ''
      to_equal '67890'


  it 'filters n elements', fn:
    expect
      pipe '1234567890':
        take 5
        join ''
      to_equal '12345'


  it 'takes 0 length slice', fn:
    expect
      pipe count 0:
        slice 1, 1
        [...?]
      to_equal
        []


  it 'takes regular slice', fn:
    expect
      pipe count 0:
        slice 0, 3
        [...?]
      to_equal
        [0, 1, 2]


  it 'takes slice from start to end of iterable', fn:
    expect
      pipe '0123456789':
        slice 5
        [...?]
      to_equal
        [...'56789']


  it 'takes end-offset slice', fn:
    expect
      pipe '0123456789':
        slice 0, -5
        [...?]
      to_equal
        [...'01234']

    expect
      pipe '0123456789':
        slice -5, -1
        [...?]
      to_equal
        [...'5678']



describe 'folding funcs', fn:
  it 'gets length', fn:
    expect
      length '1234'
      to_equal 4

    expect
      length []
      to_equal 0

    expect
      length pipe [1, 2, 2]: unique
      to_equal 2

    expect
      length 'abc' | chain 'def'
      to_equal 6

    expect
      length {}
      to_equal (-1)


  it 'checks if iterable is empty', fn:
    expect
      is_empty ''
      to_equal true

    expect
      is_empty []
      to_equal true

    expect
      is_empty 'foo'
      to_equal false

    expect
      is_empty [1]
      to_equal false

    expect
      is_empty count 1
      to_equal false

    expect
      is_empty unique '123456'
      to_equal false

    expect
      is_empty {}
      to_equal true


  it 'joins iterables with a separator', fn:
    expect
      pipe 'abc':
        chain 'def'
        join ','

      to_equal
        'a,b,c,d,e,f'


  it 'finds index of elem', fn:
    expect
      pipe [1, 2, 3]:
        find_index 2
      to_equal 1

    expect
      pipe [1, 2, 3]:
        find_index 0
      to_equal -1

    expect
      pipe [1, 2, 3]:
        find_index fn item: item == 2
      to_equal 1



describe 'reflective', fn:
  it 'is_iter', fn:
    expect
      is_iter []
      to_equal true

    expect
      is_iter ''
      to_equal true

    expect
      is_iter (unfold: 1) _
      to_equal true

    expect
      is_iter (unfold: await 1) _
      to_equal true

    expect
      is_iter 123
      to_equal false



describe 'comparison', fn:
  it 'checks if iterables are equal', fn:
    expect
      equals 'abc', 'abc'
      to_equal true

    expect
      equals 'abc', 'def'
      to_equal false

    expect
      equals 'abc', 'ab'
      to_equal false
