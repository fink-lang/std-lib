{describe, it, expect, to_equal} = import '@fink/jest'

{matches, match_all, find_index, split, replace_all, replace, rx, regex} = import './regex'


describe 'regex', fn:
  it 'matches single', fn:
    expect
      matches 'foobar', rx'.+oo.+'
      to_equal
        true

    expect
      matches 'foobar', rx'ni'
      to_equal
        false


  it 'matches multiple', fn:
    [...matches] = pipe match_all 'foobar, spam, ham', rx'(?<=\s).+?am':
      map [m]: m

    expect
      matches
      to_equal
        ['spam', 'ham']


  it 'searches', fn:
    expect
      find_index 'foobar, spam, ham', rx'(?<=\s).+?am'
      to_equal 8


  it 'splits', fn:
    expect
      split 'foobar, spam , ham', rx'\s*,\s*'
      to_equal
        ['foobar', 'spam', 'ham']


  it 'replaces all', fn:
    expect
      replace_all 'foobar, spam, ham, ni', rx',.+?am', ''
      to_equal 'foobar, ni'


  it 'replaces single', fn:
    expect
      replace 'foobar, spam, ham, ni', rx',.+?am', ''
      to_equal 'foobar, ham, ni'



describe 'string templates', fn:
  it 'creates a regex', fn:
    expect
      rx'\(.+?\)(foo{${1},${4}})'
      to_equal rx'\(.+?\)(foo{1,4})'


  it 'handles multiline commented regex', fn:
    expect
      rx'
        (?<year>\d{4})-    # year part of a date
        (?<month>\d{2})-  # month part of a date
        (?<day>\d{2})      # day part of a date
      '
      to_equal rx'(?<year>\d{4})-(?<month>\d{2})-(?<day>\d{2})'


  it 'handles escaping comment', fn:
    expect
      rx'foo\#bar'
      to_equal regex 'foo\\#bar'


  it 'handles spaces', fn:
    expect
      rx'[ ]{3}'
      to_equal regex '[ ]{3}'


